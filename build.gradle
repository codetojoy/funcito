
apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    // functional APIs
    compile group: 'com.google.guava', name: 'guava', version: 'r08'
    compile group: 'org.functionaljava', name: 'functionaljava', version: '3.0'

    // internals
    compile group: 'cglib', name: 'cglib-nodep', version: '2.2'
    compile group: 'org.objenesis', name: 'objenesis', version: '1.2'
    compile group: 'org.javassist', name: 'javassist', version: '3.14.0-GA'

    // test
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
}

// version
def propFile = new File('resources/Funcito.properties')
assert propFile.exists()
def properties = new Properties()
properties.load(new FileReader(propFile))
version = properties.getProperty('funcito.version')

println "***** working with Funcito v ${version}"

// sourceSets
sourceSets.main.java.srcDirs 'src', 'examples'
sourceSets.main.resources.srcDir 'resources'
sourceSets.test.java.srcDirs 'test'

// build src jar
task srcJar(type: Jar) {
    baseName = "${project.name}-src"

    from(sourceSets.main.java.srcDirs) {
        include '**/*.java'
    }

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.properties'
    }
    
    from('docs') {
        include 'ReadMe.txt'
        include 'License.txt'
    }
}

jar.dependsOn(srcJar)

// build binary jar
jar {
    from { 
        configurations.compile.findAll {
            if (it.name =~ /objenesis/) { it }
        } 
    }        
    from('docs') {
        include 'ReadMe.txt'
        include 'License.txt'
    }    
}


